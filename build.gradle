buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:5.3.0"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

plugins {
    id "java"
    id "com.diffplug.spotless" version "5.5.1"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "com.github.spotbugs" version "4.6.0"
    id 'jacoco'
}

apply plugin: "io.freefair.lombok"
apply plugin: "com.github.johnrengelman.shadow"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jsoup:jsoup:${project.properties['jsoup_version']}"
    implementation "com.konghq:unirest-java:${project.properties['unirest_version']}"
    implementation "ch.qos.logback:logback-core:${project.properties['logback_version']}"
    implementation "ch.qos.logback:logback-classic:${project.properties['logback_version']}"
    implementation "org.slf4j:slf4j-api:${project.properties['slf4j_version']}"
    implementation "net.sourceforge.argparse4j:argparse4j:${project.properties['argparse4j_version']}"

    testImplementation "org.mockito:mockito-core:${project.properties['mockito_version']}"
    testImplementation "org.mockito:mockito-junit-jupiter:${project.properties['mockito_version']}"
    testImplementation "org.assertj:assertj-core:${project.properties['assertj_version']}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.properties['junit_version']}"
    testImplementation "com.github.tomakehurst:wiremock-jre8:${project.properties['wiremock_version']}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.properties['junit_version']}"
}


group = "com.crawler"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "11"


jar {
    manifest {
        attributes "Main-Class": "com.crawler.Application"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


spotless {
    java {
        googleJavaFormat("1.9").aosp()
        target fileTree(rootDir) {
            include '**/*.java'
        }
    }
}

jacocoTestReport {
    dependsOn test
}
